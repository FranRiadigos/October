buildscript {
    repositories {
        jcenter()
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
        classpath 'me.tatarka:gradle-retrolambda:3.2.3'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 23
        versionCode project.hasProperty('projectVersionCode') ? projectVersionName.toInteger() : 0
        versionName project.hasProperty('projectVersionName') ? projectVersionName : "0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    lintOptions {
        quiet true
        abortOnError false
        ignoreWarnings true
        disable 'OldTargetApi'
        disable 'InvalidPackage'
    }

    buildTypes {
        debug {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':october-core')
    compile(
            "com.android.support:support-v4:23.3.0",
            "com.android.support:appcompat-v7:23.3.0",
            "com.android.support:support-annotations:23.3.0",
            "com.trello:rxlifecycle-components:0.5.0",
            "com.jakewharton:butterknife:7.0.1",
            "io.realm:realm-android:0.87.5",
            "com.google.dagger:dagger:2.1",
            "io.reactivex:rxandroid:1.0.1",
            "io.reactivex:rxjava:1.1.0"
    )
    // Testing
    testCompile 'junit:junit:4.+'
}

ext {
    // Bintray metadata
    bintrayRepo = 'maven'
    bintrayName = 'october'

    // Maven metadata
    publishedGroupId = 'com.kuassivi.october'
    libraryName = 'October'
    artifact = 'october'

    libraryDescription = 'Android MVP Framework.'

    siteUrl = project.hasProperty('projectWebsite') ? projectWebsite : ''
    gitUrl = siteUrl + '.git'
    libraryVersion = project.hasProperty('projectVersionName') ? projectVersionName : '0'

    developerId = project.hasProperty('bintray.user') ? project['bintray.user'] : 'noUser'
    developerName = project.hasProperty('developerName') ? developerName : 'noName'
    developerEmail = project.hasProperty('developerEmail') ? developerEmail : 'noEmail'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]

    mavenPackagin = "aar"

    syncMaven = false
}

if(localPropExists) {
    apply from: "${rootDir}/../bintray.gradle"
    apply from: "${rootDir}/../install.gradle"
}
